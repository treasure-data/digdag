buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'idea'
apply plugin: 'maven'

allprojects {
    group = 'io.digdag'
    version = '0.8.10'

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
        jcenter()
    }

    jacoco {
        toolVersion = "0.7.7.201606060606"
    }
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'findbugs'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                //groupId 'io.digdag'
                //artifactId 'digdag'
                from components.java
            }
        }
        repositories {
            maven {
                name 's3Release'
                if (project.version.endsWith('-SNAPSHOT')) {
                    url 's3://digdag-beta-release/maven-snapshot'
                }
                else {
                    url 's3://digdag-beta-release/maven'
                }
                credentials(AwsCredentials) {
                    accessKey "${System.env.AWS_ACCESS_KEY_ID}"
                    secretKey "${System.env.AWS_SECRET_ACCESS_KEY}"
                }
            }
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.21'
        compile 'com.google.guava:guava:19.0'

        testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-library:1.3'
        testCompile 'org.mockito:mockito-core:1.10.19'
        testCompile 'pl.pragmatists:JUnitParams:1.0.5'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    findbugs {
        effort = 'max'
        reportLevel = 'high'
        ignoreFailures = true
        excludeFilter = file("${rootProject.projectDir}/config/findbugs-exclude.xml")
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacocoTestReport {
        // exclude classes generated by Immutables
        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ['**/Immutable*'])
            })
        }
    }

    // "./gradlew check" runs jacocoTestReport
    check.dependsOn('jacocoTestReport')

    test {
        testLogging {
            info.events = ["started", "passed", "skipped", "failed", "standardOut", "standardError"]
        }

        // Shard test classes over CI workers
        if (System.env.CI_NODE_INDEX != null) {
            def n = Integer.parseInt(System.env.CI_NODE_TOTAL)
            def i = Integer.parseInt(System.env.CI_NODE_INDEX)
            include {
                // Recurse into all packages
                if (it.file.isDirectory()) {
                    return true
                }

                // Include test class based on worker index
                def included = Math.abs(it.file.hashCode()) % n == i
                if (Boolean.parseBoolean(System.env.CI_DEBUG ?: 'false')) {
                    logger.info("test '" + it.file + '" included: ' + included)
                }
                return included
            }
        }
    }

    javadoc {
        options {
            locale = 'en_US'
            encoding = 'UTF-8'
        }
    }

    task testsJar(type: Jar, dependsOn: classes) {
        classifier = 'tests'
        from sourceSets.test.output
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task deps(type: DependencyReportTask) {
    }

    //artifacts {
    //    archives testsJar, sourcesJar, javadocJar
    //}

    // Generate maven manifests for sub-modules
    task pom << {
        pom {
            project {
                parent {
                    groupId project.group
                    artifactId 'digdag'
                    version project.version
                }
            }
        }.writeTo("${delegate.project.projectDir}/pom.xml")
    }
}

jacocoTestReport {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        xml.enabled = false
        html.enabled = true
    }
    onlyIf = { true }
    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/Immutable*'])
        })
    }
}
check.dependsOn('jacocoTestReport')

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

project(':digdag-cli') {
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        manifest {
            attributes 'Implementation-Title': project.name,
                       'Implementation-Version': project.version,
                       'Implementation-Vendor-Id': project.group,
                       'Specification-Title': project.name,
                       'Specification-Version': project.version,
                       'Main-Class': 'io.digdag.cli.Main'
        }
//        include "${rootProject.projectDir}/LICENSE"
        exclude 'io/digdag/**/*.java'
        dependencies {
            exclude(dependency("org.immutables:value"))
            exclude(dependency("org.immutables:builder"))
        }
        mergeServiceFiles {
            // this block is necessary to merge META-INF/services/* files.
            // undertow is using ServiceLoader to find ExchangeAttributeBuilder
            // which is used by access logger.
        }
        // force org.weakref.jmx to use unshaded guava to keep binary size small
        exclude 'org/weakref/jmx/internal/guava/**/*'
        relocate 'org.weakref.jmx.internal.guava', 'com.google.common'
    }

    task classpath(type: Copy, dependsOn: ['jar']) {
        File dest = file("${rootProject.projectDir}/classpath")
        doFirst { dest.deleteDir() }
        from configurations.runtime + files("${project.libsDir}/${project.name}-${project.version}.jar")
        into dest
    }

    //publishing {
    //    publications {
    //        shadowJar(MavenPublication) {
    //            artifactId 'digdag'
    //            artifact cliBuildFile
    //        }
    //    }
    //}
}

def cliBuildFile = file("pkg/digdag-${project.version}.jar")

task cli(dependsOn: ':digdag-cli:shadowJar') << {
    file('pkg').mkdirs()
    cliBuildFile.write("")
    cliBuildFile.append(file("digdag-cli/src/main/sh/selfrun.sh").readBytes())
    cliBuildFile.append(file("digdag-cli/build/libs/digdag-cli-${project.version}-all.jar").readBytes())
    cliBuildFile.setExecutable(true)
}

configurations {
    releases
    rootFiles
}

artifacts {
    releases file: cliBuildFile, name: 'digdag', type: 'text', builtBy: cli
    rootFiles file: file("digdag-core/src/main/resources/io/digdag/core/version.txt"), name: 'LATEST', type: 'text'
}

uploadReleases {
    repositories {
        if (!project.version.endsWith('-SNAPSHOT')) {
            ivy {
                name 's3PackageRelease'
                artifactPattern 's3://digdag-beta-release/releases/[artifact]-[revision].[ext]'
                ivyPattern 's3://digdag-beta-release/releases/metadata/[artifact]-[revision].[ext]'
                //credentials = project(':digdag-cli').publishing.repositories.s3Release.getCredentials(AwsCredentials)
                credentials(AwsCredentials) {
                    accessKey "${System.env.AWS_ACCESS_KEY_ID}"
                    secretKey "${System.env.AWS_SECRET_ACCESS_KEY}"
                }
            }
        }
    }
}

uploadRootFiles {
    repositories {
        if (!project.version.endsWith('-SNAPSHOT')) {
            ivy {
                name 's3RootFiles'
                artifactPattern 's3://digdag-beta-release/releases/[artifact].[ext]'
                ivyPattern 's3://digdag-beta-release/releases/metadata/[artifact].[ext]'
                //credentials = project(':digdag-cli').publishing.repositories.s3Release.getCredentials(AwsCredentials)
                credentials(AwsCredentials) {
                    accessKey "${System.env.AWS_ACCESS_KEY_ID}"
                    secretKey "${System.env.AWS_SECRET_ACCESS_KEY}"
                }
            }
        }
    }
}
uploadRootFiles.dependsOn(uploadReleases)

task classpath(dependsOn: [':digdag-cli:classpath']) << { }

clean {
    delete 'classpath'
    delete 'pkg'
}

task release() {
    dependsOn subprojects.publish
    dependsOn uploadReleases
    dependsOn uploadRootFiles
    doLast {
        println("done.")
    }
}

task setVersion << {
    if (!project.hasProperty("to")) {
        throw new GradleException("Usage: ./gradlew setVersion -Pto=VERSION")
    }

    File gradle_ver = file('build.gradle')
    gradle_ver.write(gradle_ver.getText().replaceFirst("version = '(\\d+)(\\.\\d+){2}(-[\\d\\w]+)?'", "version = '${to}'"))

    List<String> files = [
        'digdag-core/src/main/resources/io/digdag/core/version.txt',
        'digdag-cli/src/main/java/io/digdag/cli/Main.java',
        'digdag-cli/src/main/java/io/digdag/cli/SelfUpdate.java',
    ]

    files.each() { path ->
        File doc = file(path)
        doc.write(doc.getText().replaceAll("0\\.[2-8]\\.(\\d+)(-[\\d\\w]+)?", "${to}"))
    }

    if (!to.endsWith('-SNAPSHOT')) {
        // update docs
        [
            'digdag-docs/src/command_reference.rst',
        ].each() { path ->
            File doc = file(path)
            doc.write(doc.getText().replaceAll("0\\.[2-8]\\.(\\d+)(-[\\d\\w]+)?", "${to}"))
        }

        // releases.rst
        File releases = file("digdag-docs/src/releases.rst")
        releases.write(releases.getText().replaceAll("# add new version here", "# add new version here\n    releases/release-${to}"))

        // release-<versionr>.rst
        file("digdag-docs/src/releases/release-${to}.rst").append("")
        "git add digdag-docs/src/releases/release-${to}.rst".execute().waitFor()
    }
}

task releaseCheck << {
    if (!file("digdag-core/src/main/resources/io/digdag/core/version.txt").getText().contains("${project.version}")) {
        throw new GradleException("digdag-core/src/main/resources/io/digdag/core/version.txt doesn't include ${project.version}")
    }
    if (!file("digdag-docs/src/releases/release-${project.version}.rst").getText().contains("${project.version}")) {
        throw new GradleException("Release note for ${project.version} doesn't exist")
    }
    if (!file("digdag-docs/src/releases.rst").getText().contains("release-${project.version}")) {
        throw new GradleException("digdag-docs/src/releases.rst doesn't include release-${project.version}")
    }
    String date = new Date().format("yyyy-MM-dd")
    if (!file("digdag-docs/src/releases/release-${project.version}.rst").getText().contains(date)) {
        throw new GradleException("digdag-docs/src/releases/release-${project.version}.rst doesn't include today's release date")
    }
    // TODO check git-ls-files includes release-<version>.rst file
    println "Ready. Run 'release' task."
}

// Generate parent maven manifest
task pom << {
    pom {
        project {
            packaging 'pom'
            properties {
                project {
                    build {
                        sourceEncoding 'UTF-8'
                    }
                }
            }
            repositories {
                repository {
                    id 'jcenter'
                    url 'http://jcenter.bintray.com'
                    releases {
                        enabled 'true'
                    }
                }
            }
            modules {
                subprojects.each {
                    module it.name
                }
            }
        }
    }.withXml {
        // XXX (dano): http://stackoverflow.com/a/27978130
        def build = asNode().appendNode('build', '')
        def plugins = build.appendNode('plugins', '')

        def compiler = plugins.appendNode('plugin', '')
        compiler.appendNode('groupId', 'org.apache.maven.plugins')
        compiler.appendNode('artifactId', 'maven-compiler-plugin')
        compiler.appendNode('version', '3.5.1')
        def compiler_configuration = compiler.appendNode('configuration', '')
        compiler_configuration.appendNode('source', sourceCompatibility)
        compiler_configuration.appendNode('target', targetCompatibility)

        def enforcer = plugins.appendNode('plugin', '')
        enforcer.appendNode('groupId', 'org.apache.maven.plugins')
        enforcer.appendNode('artifactId', 'maven-enforcer-plugin')
        enforcer.appendNode('version', '1.4.1')
        def enforcer_executions = enforcer.appendNode('executions', '')
        def enforcer_execution = enforcer_executions.appendNode('execution', '')
        enforcer_execution.appendNode('id', 'enforce')
        def enforcer_execution_configuration = enforcer_execution.appendNode('configuration', '')
        def enforcer_rules = enforcer_execution_configuration.appendNode('rules', '')
        enforcer_rules.appendNode('requireUpperBoundDeps', '')
    }.writeTo("pom.xml")
}

task runServer(type: JavaExec) {
    classpath = project(":digdag-cli").sourceSets.main.runtimeClasspath
    main = 'io.digdag.cli.Main'
    args 'server',
            '-c', '/dev/null',
            '-m',
            '-H', 'Access-Control-Allow-Origin=http://localhost:8080',
            '-H', 'Access-Control-Allow-Headers=origin, content-type, accept, authorization',
            '-H', 'Access-Control-Allow-Credentials=true',
            '-H', 'Access-Control-Allow-Methods=GET, POST, PUT, DELETE, OPTIONS, HEAD',
            '-H', 'Access-Control-Max-Age=1209600'
}
